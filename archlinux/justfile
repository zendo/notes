set shell := ["bash", "-uc"]

hostname := "arch"
username := "iab"
password := "123"

# iwctl or nmtui
# station wlan0 get-networks
# station wlan0 connect SSID

# cfdisk / lsblk
# pacman -Sy git just fzf micro

_default:
    @just --choose --unsorted

# ==================== LiveCD 环境阶段 ====================

[group('livecd')]
l0-partition:
    #!/usr/bin/env bash
    parted /dev/vda mklabel gpt
    parted /dev/vda mkpart ESP fat32 1MiB 513MiB
    parted /dev/vda set 1 boot on
    parted /dev/vda mkpart primary btrfs 513MiB 100%
    mkfs.fat -F32 /dev/vda1
    mkfs.btrfs -f /dev/vda2
    mount /dev/vda2 /mnt
    mkdir /mnt/efi
    mount /dev/vda1 /mnt/efi

[group('livecd')]
l1-enable-NTP:
    timedatectl set-ntp true

[group('livecd')]
l2-change-mirrors:
    sed -i "1i Server = https://mirror.sjtu.edu.cn/archlinux/\$repo/os/\$arch" /etc/pacman.d/mirrorlist
    sed -i "1i Server = https://mirrors.sustech.edu.cn/archlinux/\$repo/os/\$arch" /etc/pacman.d/mirrorlist

[group('livecd')]
l3-pacstrap:
    pacstrap /mnt base base-devel linux linux-firmware efibootmgr bash-completion git just fzf micro

[group('livecd')]
l4-genfstab:
    genfstab -U /mnt /mnt/efi >> /mnt/etc/fstab

[group('livecd')]
l5-cp-notes-chroot:
    cp -r ~/notes /mnt
    arch-chroot /mnt

# ==================== Chroot 环境阶段 ====================

[group('chroot')]
c1-timezone:
    ln -sf /usr/share/zoneinfo/Asia/Shanghai /etc/localtime
    hwclock --systohc

[group('chroot')]
c2-localization:
    echo 'en_US.UTF-8 UTF-8' >> /etc/locale.gen
    echo 'zh_CN.UTF-8 UTF-8' >> /etc/locale.gen
    locale-gen
    echo 'LANG=zh_CN.UTF-8' > /etc/locale.conf

[group('chroot')]
c3-hosts:
    #!/usr/bin/env bash
    echo "{{ hostname }}" > /etc/hostname
    tee -a /etc/hosts <<EOF
    127.0.1.1  {{ hostname }}
    EOF

[group('chroot')]
c4-core-apps:
    pacman -S mesa dhcpcd netctl iw dialog wpa_supplicant \
              networkmanager bind-tools net-tools dosfstools \
              ntfs-3g btrfs-progs sudo micro \
              wget curl expac zsh
    systemctl enable dhcpcd.service
    systemctl enable NetworkManager.service
    systemctl enable systemd-timesyncd.service

[group('chroot')]
c5-useradd:
    #!/usr/bin/env bash
    echo "root:{{ password }}" | chpasswd
    useradd -m -G wheel "{{ username }}"
    echo "{{ username }}:{{ password }}" | chpasswd
    tee -a /etc/sudoers <<EOF
    %wheel  ALL=(ALL:ALL)   NOPASSWD:SETENV: ALL
    EOF

[group('chroot')]
c6-grub:
    pacman -S --noconfirm grub os-prober
    grub-install --target=x86_64-efi --efi-directory=/efi --bootloader-id=GRUB
    grub-mkconfig -o /boot/grub/grub.cfg

# ==================== 重启后配置阶段 ====================

[group('post')]
archlinuxcn-repo:
    #!/usr/bin/env bash
    sudo tee -a /etc/pacman.conf <<EOF
    [archlinuxcn]
    Server = https://repo.archlinuxcn.org/\$arch
    EOF
    sudo pacman -Syy && sudo pacman -S --noconfirm archlinuxcn-keyring

[group('post')]
apps:
    sudo pacman -S --noconfirm wayland libinput a52dec libmad \
                   man-pages-zh_cn firefox-i18n-zh-cn \
                   paru pamac flatpak

[group('post')]
fonts:
    sudo pacman -S ttf-hack ttf-jetbrains-mono ttf-maplemono \
                   ttf-noto-sans-cjk-vf ttf-noto-serif-cjk-vf

[group('post')]
gnome:
    sudo pacman -S gnome seahorse refine \
         gnome-browser-connector gnome-power-manager
    sudo systemctl enable gdm.service

[group('post')]
kde:
    sudo pacman -S plasma plasma-wayland-session \
                   konsole dolphin ark gwenview xdg-desktop-portal
    sudo systemctl enable sddm.service

[group('post')]
fcitx:
    sudo pacman -S fcitx5-im fcitx5-chinese-addons

[group('post')]
amd:
    sudo pacman -S --noconfirm amd-ucode mesa xf86-video-amdgpu vulkan-radeon

[group('post')]
intel:
    sudo pacman -S --noconfirm intel-ucode mesa vulkan-intel

[group('post')]
nvidia:
    sudo pacman -S --noconfirm nvidia-open nvidia-settings

[group('post')]
virt-guset:
    sudo pacman -S spice-vdagent
